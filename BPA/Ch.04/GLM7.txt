
    
    model {
    
      mu ~ dnorm(0, 0.01)
      
      b1 ~ dnorm(0, 0.01)
      b2 ~ dnorm(0, 0.01)
      
      for (j in 1:n_sites) {
        a[j] ~ dnorm(0, a_tau)
      }
      a_tau <- 1 / (a_sigma * a_sigma)
      a_sigma ~ dunif(0, 3)

      for (i in 1:n_years) {
        e[i] ~ dnorm(0, e_tau)
      }
      e_tau <- 1 / (e_sigma * e_sigma)
      e_sigma ~ dunif(0, 1)
      
      for (i in 1:n_obs) {
        g[i] ~ dnorm(0, g_tau)
      }
      g_tau <- 1 / (g_sigma * g_sigma)
      g_sigma ~ dunif(0, 1)
      
      for (i in 1:n_years) {
    
        for (j in 1:n_sites) {
    
          C[i, j] ~ dpois(lambda[i, j])
          log(lambda[i, j]) <- mu + b1 * first[i, j] + b2 * year[i] + a[j] + e[i] + g[obs[i, j]]
    
        }
    
      }
    
    }
    
    
