
model {

# -------------------------------------------------
# Parameters:
# s: true survival probability
# r: recovery probability
# p: detection probability
# -------------------------------------------------
# States (S):
# 1 alive in study area
# 2 recently dead (and recovered)
# 3 recently dead, but not recovered, or dead (absorbing)
# Observations (O):
# 1 seen alive
# 2 recovered dead
# 3 neither seen nor recovered
# -------------------------------------------------

# Priors and constraints

for (t in 1:(n.occasions-1)){
   s[t] ~ dbeta(1, 1)
   p[t] ~ dbeta(1, 1)
}

for (t in 1:2){
   r[t] ~ dbeta(1, 1)
}



# Define state-transition and observation matrices 	

   # Define probabilities of state S(t+1) given S(t)
   
   for (t in 1:(n.occasions-1)) {
      
      ps[1,t,1] <- s[t]
      ps[2,t,1] <- 0
      ps[2,t,2] <- 0
      ps[2,t,3] <- 1
      ps[3,t,1] <- 0
      ps[3,t,2] <- 0
      ps[3,t,3] <- 1

      # Define probabilities of O(t) given S(t)

      po[1,t,1] <- p[t]
      po[1,t,2] <- 0
      po[1,t,3] <- 1-p[t] 
      po[2,t,1] <- 0
      po[2,t,2] <- 1
      po[2,t,3] <- 0
      po[3,t,1] <- 0
      po[3,t,2] <- 0
      po[3,t,3] <- 1
      
    } #t
      
    rfix <- 0
    
    for (t in 1:2) {
      
      ps[1,t,2] <- 1-s[t] * rfix
      ps[1,t,3] <- 1-s[t] * (1-rfix)
      
    }  
     
    for (t in 3:4) {
      
      ps[1,t,2] <- (1-s[t]) * r[t-2]
      ps[1,t,3] <- (1-s[t]) * (1-r[t-2])
      
    } 
  
  
  
  # Likelihood 
  for (i in 1:nind) {

   # Define latent state at first capture
   z[i,f[i]] <- y[i,f[i]]
   
   for (t in (f[i]+1):n.occasions) {
   
      # State process: draw S(t) given S(t-1)
      z[i,t] ~ dcat(ps[z[i,t-1], t-1,])
      
      # Observation process: draw O(t) given S(t)
      y[i,t] ~ dcat(po[z[i,t], t-1,])
      
    } #t
   
  } #i
}

