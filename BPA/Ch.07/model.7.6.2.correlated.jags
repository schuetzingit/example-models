

model {
    
    # priors and constraints
    
    for (i in 1:nInd) {
      for (t in f[i]:(nOcc - 1)) {
        logit(phi[i, t]) <- phi.eta[t, group[i]]
        p[i, t] <- p.g[group[i]]
      }
    }

    # for survival parameters
    
    for (t in 1:(nOcc - 1)){
      phi.eta[t, 1:g] ~ dmnorm(phi.mu[], omega[ , ])
    }
      
    for (u in 1:g) {
      phi.g[u] ~ dunif(0, 1) 
      phi.mu[u] <- logit(phi.g[u])
    }
    
    omega[1:g, 1:g] ~ dwish(R[, ], df)
    sigma[1:g, 1:g] <- inverse(omega[, ])
    
    # for recapture parameters
    
    for (u in 1:g){
      p.g[u] ~ dunif(0, 1)
    }
    

    # likelihood
    
    for (i in 1:nInd) {
    
      # define latent state at first capture
      
      z[i, f[i]] <- 1
      
      for (t in (f[i] + 1):nOcc) {
      
        # state process
        
        z[i, t] ~ dbern(mu1[i, t])
        mu1[i, t] <- phi[i, t - 1] * z[i, t - 1]  
        
        # observation process
        
        y[i, t] ~ dbern(mu2[i, t])
        mu2[i, t] <- p[i, t - 1] * z[i, t]      
        
      }
  
    }
  
}

