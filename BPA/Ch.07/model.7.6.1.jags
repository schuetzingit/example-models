

model {
    
    for (i in 1:nInd) {
      for (t in f[i]:(nOcc - 1)) {
        logit(phi[i, t]) <- beta[group[i]] + gamma[t]
        p[i, t] <- p.g[group[i]]
      }
    }

    for (t in 1:(nOcc - 1)){
      gamma[t] ~ dnorm(0, 0.01)
      phi.g1[t] <- ilogit(gamma[t])
      phi.g2[t] <- ilogit(gamma[t] + beta[2])
    }
    
    beta[1] <- 0
    beta[2] ~ dnorm(0, 0.01)
      
    for (u in 1:g){
      p.g[u] ~ dunif(0, 1)
    }
    

    # likelihood
    
    for (i in 1:nInd) {
    
      # define latent state at first capture
      
      z[i, f[i]] <- 1
      
      for (t in (f[i] + 1):nOcc) {
      
        # state process
        
        z[i, t] ~ dbern(mu1[i, t])
        mu1[i, t] <- phi[i, t - 1] * z[i, t - 1]  
        
        # observation process
        
        y[i, t] ~ dbern(mu2[i, t])
        mu2[i, t] <- p[i, t - 1] * z[i, t]      
        
      }
  
    }
  
}

